<!DOCTYPE html>
<html>
	<body onload="webGLStart();" style="width:100%; height:100%;">
		<center><canvas id="canvas" style="border: none;" width="1024px" height="768px"></canvas><br>
		<input type="radio" name="mode" value="0" checked> No AO<br>
		<input type="radio" name="mode" value="1" > SSAO<br>
		<input type="radio" name="mode" value="2" > SSAO map<br>
		<input type="radio" name="mode" value="3" > SSDO<br>
		<input type="radio" name="mode" value="4" > SSDO map<br>
		<input type="radio" name="mode" value="5" > G-buffer<br>
		Sky: <input class="jscolor" id="skyColor" value="7EC0EE"><br>
		Sun: <input class="jscolor" id="sunColor" value="FFD440"><br></center>
		<script src="jscolor.min.js"></script>
		<script type="text/javascript" src="gltools.js"></script>
		<script type="text/javascript" src="gl.js"></script>
		<script type="text/javascript" src="models.js"></script>
		<script id="blurY-fs" type="x-shader/x-fragment">
			precision lowp float;
			precision lowp sampler2D;
			uniform sampler2D texture;
			varying vec2 texmap;

			vec4 blur(in vec2 pos){
				const float radius = 20.0;
				const vec2 dir = vec2(0.0,0.003);

				const float INV_SQRT_2PI_X3 = 1.1968268412042980338198381798031;
				vec4 result = vec4(0.0);
				vec2 dpos = vec2(0,0);
				float expFactor = -4.5 / radius / radius;
				for(float x=0.0; x<=radius; x+=1.0){
					vec4 color = texture2D(texture, pos - dpos) + texture2D(texture, pos + dpos);
					result += color * exp(expFactor * x * x);
					dpos += dir;
				}
				return INV_SQRT_2PI_X3 / radius * result;
			}

			void main(void) {
				gl_FragColor = blur(texmap);
			}
		</script>
		<script id="blurY-vs" type="x-shader/x-vertex">
			attribute vec2 aVertexPosition;
			attribute vec2 aVertexTexmap;
			varying vec2 texmap;

			void main(void) {
				texmap=aVertexTexmap;
				gl_Position = vec4(aVertexPosition, 0.0, 1.0);
			}
		</script>
		<script id="blurX-fs" type="x-shader/x-fragment">
			precision lowp float;
			precision lowp sampler2D;
			uniform sampler2D texture;
			varying vec2 texmap;

			vec4 blur(in vec2 pos){
				const float radius = 20.0;
				const vec2 dir = vec2(0.003, 0.0);

				const float INV_SQRT_2PI_X3 = 1.1968268412042980338198381798031;

				vec4 result = vec4(0.0);
				vec2 dpos = vec2(0.0);
				float expFactor = -4.5 / radius / radius;
				for(float x=0.0; x<=radius; x+=1.0){
					vec4 color = texture2D(texture, pos - dpos) + texture2D(texture, pos + dpos);
					result += color * exp(expFactor * x * x);
					dpos += dir;
				}
				return INV_SQRT_2PI_X3 / radius * result;
			}

			void main(void) {
				gl_FragColor = blur(texmap);
			}
		</script>
		<script id="blurX-vs" type="x-shader/x-vertex">
			attribute vec2 aVertexPosition;
			attribute vec2 aVertexTexmap;
			varying vec2 texmap;

			void main(void) {
				texmap=aVertexTexmap;
				gl_Position = vec4(aVertexPosition, 0.0, 1.0);
			}
		</script>
		<script id="shader2d-fs" type="x-shader/x-fragment">
			precision lowp float;
			precision lowp sampler2D;

			uniform sampler2D colorTexture;
			uniform sampler2D positionTexture;
			uniform sampler2D normalTexture;
			uniform sampler2D ssdoTexture;
			uniform vec3 skyColor;
			uniform vec3 sunColor;
			uniform int mode;

			varying vec2 texmap;

			float gamma = 2.2;
			vec4 gammaCorrect(in vec4 v){
				return vec4(pow(v.rgb,vec3(1.0/gamma)), v.a);
			}
			vec4 textureCorrect(in vec4 v){
				return vec4(pow(v.rgb,vec3(gamma)), v.a);
			}

			void main(void) {
				vec4 color = textureCorrect(texture2D(colorTexture, texmap));
				vec3 position = texture2D(positionTexture, texmap).xyz;
				vec3 normal = texture2D(normalTexture, texmap).xyz;
				vec4 ssdo = textureCorrect(texture2D(ssdoTexture, texmap));

				vec3 L = normalize(vec3(0.0,4.0,-3.0)-position);
				vec3 E = normalize(position);

				const float shininess = 64.0;
				float specIntensity = 0.0;//clamp(pow(max(dot(normal,normalize(L - E)),0.0),shininess/4.0),0.0,1.0);
				vec4 specColor = vec4(1.0);

				float lightIntensity = clamp(dot(normal, L)*1.7,0.0,1.0);
				vec4 lightColor = vec4(sunColor,1.0);

				if(dot(normal, L)<=0.0){
					specIntensity=0.0;
					lightIntensity=0.0;
				}

				float ambientIntensity = 0.6;
				vec4 ambientColor = vec4(skyColor,1.0);

				color *= (lightIntensity*lightColor+ambientIntensity*ambientColor); //diffuse and ambient
				color += specColor*specIntensity; //spectacular
				if(mode==1)
					color *= ssdo.a; //add SSAO
				if(mode==3)
					color *= ssdo; //add SSDO

				if(mode==2)
					color = vec4(ssdo.a); //SSAO map mode
				if(mode==4)
					color = ssdo; //SSDO map mode

				if(mode==5) {
					vec2 factor = (texmap - 0.5) * 64.0;
					color = vec4(mix(
						mix(
							color.rgb,
							ssdo.rgb,
							clamp(factor.x,0.0,1.0)
						),
						mix(
							normal,
							position,
							clamp(factor.x,0.0,1.0)
						),
						clamp(factor.y,0.0,1.0)
					), 1.0);
				}

				gl_FragColor = gammaCorrect(color);
				gl_FragColor.a=1.0;
			}
		</script>
		<script id="shader2d-vs" type="x-shader/x-vertex">
			attribute vec2 aVertexPosition;
			attribute vec2 aVertexTexmap;
			varying vec2 texmap;

			void main(void) {
				texmap=aVertexTexmap;
				gl_Position = vec4(aVertexPosition, 0.0, 1.0);
			}
		</script>
		<script id="ssdo-fs" type="x-shader/x-fragment">
			precision lowp float;
			precision lowp sampler2D;
			uniform sampler2D colorTexture;
			uniform sampler2D positionTexture;
			uniform sampler2D normalTexture;
			uniform mat4 projectionMatrix;
			varying vec2 texmap;

			float gamma = 2.2;
			vec4 gammaCorrect(in vec4 v){
				return vec4(pow(v.rgb,vec3(1.0/gamma)), v.a);
			}
			vec4 textureCorrect(in vec4 v){
				return vec4(pow(v.rgb,vec3(gamma)), v.a);
			}

			float rand(in vec2 co) {
				return fract(sin(dot(normalize(co.xy), vec2(12.9898, 78.233))) * 43758.5453);
			}
			const int samples = 8;
			vec3 rand3(vec3 co, int i){
				co/=10000.0;
				float ci = float(i)/float(samples);
				return vec3(
					rand(vec2(
						rand(co.xy),
						rand(vec2(co.z,ci)))
					),
					rand(vec2(
						rand(co.xz),
						rand(vec2(co.y,ci)))
					),
					rand(vec2(
						rand(vec2(co.x,ci)),
						rand(co.yz))
					));
			}
			void main(void) {
				vec4 color = textureCorrect(texture2D(colorTexture, texmap));
				vec3 position = texture2D(positionTexture, texmap).xyz;
				vec3 normal = texture2D(normalTexture, texmap).xyz;

				vec4 ssdo = vec4(0.0);
				vec4 ssdo_amb = vec4(0.0);
				float ssao = 0.0;
				int count = 0;
				const float radius = 1.0;
				const float max_distance = 2.0;
				vec4 sky = vec4(1.0);
				for(int i=0;i<samples;i++){
					vec3 occDir = normalize(rand3(position,i) * 2.0 - 1.0);
					if(dot(normal,occDir)<=0.0) continue;
					vec3 occDiff = occDir * radius * float(i+1) / float(samples);
					vec3 occ = occDiff + position;
					vec4 occ2screen = projectionMatrix * vec4(occ, 1.0);
					occ2screen /= occ2screen.w;

					vec2 occTexCoord = clamp(occ2screen.xy*0.5+0.5,0.0,1.0);
					vec3 occPos = texture2D(positionTexture, occTexCoord).xyz;
					vec3 occNormal = texture2D(normalTexture, occTexCoord).xyz;
					vec4 occColor = textureCorrect(texture2D(colorTexture, occTexCoord));

					float distance =  abs(occPos.z - position.z) / max_distance;
					float invisibility = ((occPos.z >= occ.z) ? 1.0 : 0.0) * smoothstep(1.0, 0.0, distance);
					float geometryFactor = max(0.0,dot(occDir, normal))*max(0.0,dot(-occDir,occNormal));
					if(occTexCoord.x<0.0 || occTexCoord.x > 1.0 || occTexCoord.y < 0.0 || occTexCoord.y > 1.0)
						invisibility = 0.0;

					ssdo += mix(sky, sky*occColor * geometryFactor, invisibility);
					ssdo_amb += occColor * geometryFactor;
					ssao += invisibility;

					count++;
				}
				ssdo = max(vec4(0.0),ssdo/max(float(count),1.0));
				ssdo_amb = max(vec4(0.0),ssdo_amb/max(float(count),1.0));
				ssao = 1.0-ssao/max(float(count),1.0);
				gl_FragColor=gammaCorrect(ssdo);
				gl_FragColor.a=pow(ssao,0.5);
			}
		</script>
		<script id="ssdo-vs" type="x-shader/x-vertex">
			attribute vec2 aVertexPosition;
			attribute vec2 aVertexTexmap;
			varying vec2 texmap;

			void main(void) {
				texmap=aVertexTexmap;
				gl_Position = vec4(aVertexPosition, 0.0, 1.0);
			}
		</script>
		<script id="shader3d-fs" type="x-shader/x-fragment">
			#extension GL_EXT_draw_buffers : require
			precision lowp float;
			precision lowp sampler2D;
			varying vec2 texmap;
			varying vec3 position;
			varying vec3 normal;
			varying vec3 normal2;

			void main(void) {
				if(normal2.z>0.5)
					gl_FragData[0] = vec4(1.0);
				else if(normal2.z>-0.5)
					gl_FragData[0] = vec4(0.0,1.0,0.0,1.0);
				else
					gl_FragData[0] = vec4(1.0, 0.0, 0.0, 1.0);
				gl_FragData[1]=vec4(position,1.0);
				gl_FragData[2]=vec4(normal,1.0);
			}
		</script>
		<script id="shader3d-vs" type="x-shader/x-vertex">
			attribute vec3 aVertexPosition;
			attribute vec3 aVertexNormal;
			attribute vec2 aVertexTexmap;
			attribute vec3 aVertexTangent;
			attribute vec3 aVertexBinormal;
			varying vec2 texmap;
			varying vec3 position;
			varying vec3 normal;
			varying vec3 normal2;
			uniform mat4 uMVMatrix;
			uniform mat4 uNMVMatrix;
			uniform mat4 uPMatrix;

			mat3 transpose(in mat3 inMatrix) {
				vec3 i0 = inMatrix[0];
				vec3 i1 = inMatrix[1];
				vec3 i2 = inMatrix[2];

				mat3 outMatrix = mat3(
							 vec3(i0.x, i1.x, i2.x),
							 vec3(i0.y, i1.y, i2.y),
							 vec3(i0.z, i1.z, i2.z)
							 );

				return outMatrix;
			}
			void main(void) {
				texmap = aVertexTexmap;
				normal2 = aVertexNormal;
				vec3 norm=normalize((uNMVMatrix* vec4(aVertexNormal, 1.0)).xyz);
				// vec3 tangent=normalize((uNMVMatrix* vec4(aVertexTangent, 1.0)).xyz);
				// vec3 binorm=normalize((uNMVMatrix* vec4(aVertexBinormal, 1.0)).xyz);
				// mat3 TBN = transpose(mat3(
				// 	tangent,
				// 	binorm,
				// 	norm
				// ));
				vec4 position4 = uMVMatrix * vec4(aVertexPosition, 1.0);
				position = position4.xyz;
				normal = norm;
				gl_Position = uPMatrix * position4;
			}
		</script>
		<script type="text/javascript">
			"use strict";

			function initBuffers() {
				var vertices = cubeV().concat([
					2.0,  0.0, -2.0,
					-2.0,  0.0, -2.0,
					-2.0, 0.0,  2.0,

					2.0, 0.0,  2.0,
					2.0, 0.0, -2.0,
					-2.0, 0.0,  2.0,


					-1.0,  1.0, 1.0,
					1.0,  1.0, 1.0,
					-1.0, 0.0,  2.0,

					1.0, 1.0, 1.0,
					1.0, 0.0, 2.0,
					-1.0, 0.0, 2.0,
				])
				var v2 = cubeV(0.1);
				for(var i=0; i<v2.length; i+=3){
					v2[i+1]+=0.5;
					v2[i+2]+=1.15;
				}
				vertices = vertices.concat(v2);
				for(var i=0; i<vertices.length; i+=3){
					vertices[i+2]-=1.0;
				}
				renderer3d.addBuffer(vertices,3,shader3d.aVertexPosition);

				var normals = cubeN().concat([
					0.0, 1.0, 0.0,
					0.0, 1.0, 0.0,
					0.0, 1.0, 0.0,

					0.0, 1.0, 0.0,
					0.0, 1.0, 0.0,
					0.0, 1.0, 0.0,


					0.0, -1.0, -1.0,
					0.0, -1.0, -1.0,
					0.0, -1.0, -1.0,

					0.0, -1.0, -1.0,
					0.0, -1.0, -1.0,
					0.0, -1.0, -1.0,
				]).concat(cubeN());
				renderer3d.addBuffer(normals,3,shader3d.aVertexNormal);

				var texmap = cubeT().concat([
					2.0,  0.0,
					0.0,  0.0,
					0.0,   2.0,

					2.0,   2.0,
					2.0,  0.0,
					0.0,  2.0,


					0.0,  0.0,
					2.0,  0.0,
					0.0,   2.0,

					2.0,  0.0,
					2.0,   2.0,
					0.0,  2.0,
				]).concat(cubeT());
				renderer3d.addBuffer(texmap,2,shader3d.aVertexTexmap);

				var buf = generateTangent(vertices, texmap);
				renderer3d.addBuffer(buf.tangents,3,shader3d.aVertexTangent);
				renderer3d.addBuffer(buf.binormals,3,shader3d.aVertexBinormal);
			}

			var gl;
			var renderer3d;
			var shader3d;
			var fbo3d;
			var texColor3d;
			var texPos3d;
			var texNorm3d;
			var bufDepth3d;
			var ssdoFilter;
			var blurXFilter;
			var blurYFilter;
			var screenFilter;
			var counter = 0;
			function tick(){
				requestAnimFrame(tick);
				fbo3d.bind();
				shader3d.bind();
				renderer3d.draw();
				fbo3d.unbind();
				if(counter%2==0){
					ssdoFilter.draw();
					blurXFilter.draw();
					blurYFilter.draw();
				}
				screenFilter.draw();
				counter++;
			}

			var angleX=0.0;
			var angleY=0.0;
			var radios;
			var skyColor;
			var sunColor;
			function webGLStart() {
				radios=document.getElementsByName('mode');
				skyColor=document.getElementById('skyColor');
				sunColor=document.getElementById('sunColor');
				var canvas = document.getElementById("canvas");
				canvas.onmousedown = handleMouseDown;
				document.onmouseup = handleMouseUp;
				document.onmousemove = handleMouseMove;
				canvas.ontouchstart = function(e){
					if(e.preventDefault)
						e.preventDefault();
					handleMouseDown(e.changedTouches[0]);
				};
				canvas.ontouchend = function(e){
					if(e.preventDefault)
						e.preventDefault();
					handleMouseUp(e.changedTouches[0]);
				};
				canvas.ontouchmove = function(e){
					if(e.preventDefault)
						e.preventDefault();
					handleMouseMove(e.changedTouches[0]);
				};
				gl = initGL(canvas);
				gl.clearColor(0.0, 0.0, 0.0, 1.0);
				gl.viewport(0, 0, gl.viewportWidth, gl.viewportHeight);
				gl.enable(gl.CULL_FACE);
				shader3d = new ShaderProgram("shader3d",gl, function(shaderProgram){
					var pMatrix = mat4.create();
					mat4.perspective(45, gl.viewportWidth / gl.viewportHeight, 0.1, 100.0, pMatrix);
					gl.uniformMatrix4fv(shaderProgram.uPMatrix, false, pMatrix);
					var mvMatrix = mat4.create();
					mat4.identity(mvMatrix);
					mat4.translate(mvMatrix, [0.0,  0.0,-3.0]);
					mat4.rotateX(mvMatrix, -1.57079632679+angleY);
					mat4.rotateZ(mvMatrix, angleX+3.14);
					gl.uniformMatrix4fv(shaderProgram.uMVMatrix, false, mvMatrix);
					var nmvMatrix=mvMatrix;
					mat4.inverse(nmvMatrix);
					mat4.transpose(nmvMatrix);
					gl.uniformMatrix4fv(shaderProgram.uNMVMatrix, false, nmvMatrix);
				});
				shader3d.addAttribute("aVertexPosition");
				shader3d.addAttribute("aVertexNormal");
				shader3d.addAttribute("aVertexTexmap");
				shader3d.addAttribute("aVertexTangent");
				shader3d.addAttribute("aVertexBinormal");
				shader3d.addUniform("uPMatrix");
				shader3d.addUniform("uMVMatrix");
				shader3d.addUniform("uNMVMatrix");

				renderer3d = new Renderer(gl);

				texColor3d = new Texture(null, gl, 0, 1024, 1024);
				texPos3d = new Texture(null, gl, TextureFlags.FLOAT, 1024, 1024);
				texNorm3d = new Texture(null, gl, TextureFlags.FLOAT, 1024, 1024);
				bufDepth3d = new RenderBuffer(gl, 1024, 1024, true);
				fbo3d = new FrameBuffer(gl);
				fbo3d.addTarget(texColor3d);
				fbo3d.addTarget(texPos3d);
				fbo3d.addTarget(texNorm3d);
				fbo3d.addRenderbuffer(bufDepth3d);

				ssdoFilter = new TextureFilter(1024,1024,{colorTexture:texColor3d,positionTexture:texPos3d,normalTexture:texNorm3d},"ssdo",gl,function(shaderProgram){
					var pMatrix = mat4.create();
					mat4.perspective(45, gl.viewportWidth / gl.viewportHeight, 0.1, 100.0, pMatrix);
					gl.uniformMatrix4fv(shaderProgram.projectionMatrix, false, pMatrix);
				});
				ssdoFilter.shader.addUniform("projectionMatrix");

				blurXFilter = new TextureFilter(1024,1024,{texture:ssdoFilter.output},"blurX",gl);
				blurYFilter = new TextureFilter(1024,1024,{texture:blurXFilter.output},"blurY",gl);
				screenFilter = new Filter({colorTexture:texColor3d,positionTexture:texPos3d,normalTexture:texNorm3d,ssdoTexture:blurYFilter.output},"shader2d",gl,
				function(shaderProgram){
					for (var i = 0, length = radios.length; i < length; i++) {
						gl.uniform3f(shaderProgram.skyColor, skyColor.jscolor.rgb[0]/255.0, skyColor.jscolor.rgb[1]/255.0, skyColor.jscolor.rgb[2]/255.0);
						gl.uniform3f(shaderProgram.sunColor, sunColor.jscolor.rgb[0]/255.0, sunColor.jscolor.rgb[1]/255.0, sunColor.jscolor.rgb[2]/255.0);
						if (radios[i].checked) {
							gl.uniform1i(shaderProgram.mode, radios[i].value);
							break;
						}
					}
				});
				screenFilter.shader.addUniform("mode");
				screenFilter.shader.addUniform("skyColor");
				screenFilter.shader.addUniform("sunColor");

				initBuffers();

				tick();
			}
			var mouseDown = false;
			var lastMouseX = null;
			var lastMouseY = null;
			function handleMouseDown(event) {
				if(event.preventDefault)
					event.preventDefault();
				mouseDown = true;
				lastMouseX = event.clientX;
				lastMouseY = event.clientY;
				return false;
			}

			function handleMouseUp(event) {
				mouseDown = false;
			}

			function handleMouseMove(event) {
				if (!mouseDown) {
					return;
				}
				var newX = event.clientX;
				var newY = event.clientY;

				var deltaX = newX - lastMouseX;
				var deltaY = newY - lastMouseY;

				angleX+=deltaX / 573.0;
				angleY+=deltaY / 573.0;

				lastMouseX = newX
				lastMouseY = newY;
			}
		</script>

	</body>
</html>
